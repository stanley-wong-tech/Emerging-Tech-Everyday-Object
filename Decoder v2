/* ***********************************************************
 *  Morse Code Translater
 *  By Ricardo Moreno Jr. 
 *  created 01/15/2018
 *  
 *  Inspired by learnelectronics
 *  https://www.youtube.com/watch?v=3WjNiJOcVaM
 *  Nick B 
 *  https://pastebin.com/SM9jmjdG
 *  
 *  From Wikipedia
 *  length of a dot is 1 unit
 *  length of a dash is 3 units
 *  length of a space between parts of the same letter is 1 unit
 *  length of a space between letters is 3 units
 *  length of a space between words is seven units
 *  
 *  The Dot or Dash are commonly referred to as Dit or Dah
 * *********************************************************** */

//Global Constants Other variables
const int buzzerPin = 13;          //set buzzer pin
const int ledPin = 6;              //set LED pin

//LCD Display
#include <LiquidCrystal.h>
LiquidCrystal lcd(12, 11, 5, 4, 3, 2);

//Global Variables
String strInput = ""; 
  
//Morse Code Specific delays
int mcUnit = 75;                    //length of time in milliseconds
                                    //   mcUnit is the only value that
                                    //   needs adjusting
int dit = mcUnit;                   //dit equals 1 unit of time
int dah = 3 * mcUnit;               //dah equals 3 units of time
int partsDelay = mcUnit;            //1 unit between "dits" 
int letterDelay = 3 * mcUnit;       //3 units between letters
int wordDelay = 7 * mcUnit;         //7 units between words

/* ***********************************************************
 *                         Global Arrays
 * *********************************************************** */
 //Recall Arrays start from 0
String letters[] = {
  ".-", "-...", "-.-.", "-..",      // A-D
  ".", "..-.", "--.", "....",       // E-H
  "..", ".---", "-.-", ".-..",      // I-L
  "--", "-.", "---", ".--.",        // M-P
  "--.-", ".-.", "...", "-",        // Q-T
  "..-", "...-", ".--", "-..-",     // U-X
  "-.--", "--.."                    // Y-Z
};

String numbers[] = {
  "-----", ".----", "..---",       //0-2 
  "...--", "....-", ".....",       //3-5
  "-....", "--...", "---..",       //6-8
  "----."};                        //9

String Lpunctuation[] = {
  "-.-.--",   //! -33
  ".-..-.",   //" -34
  "X",        //# -35
  "...-..-",  //$ -36
  "X",        //% -37  
  ".-...",    //& -38
  ".----.",   //' -39
  "-.--.",    //( -40
  "-.--.-",   //) -41
  "X",        //* -42
  ".-.-.",    //+ -43
  "X",        //, -44
  "-....-",   //- -45
  ".-.-.-",   //. -46
  "-..-.",    /// -47
};

String Upunctuation[] = {
  "---...",   //: -58
  "-.-.-.",   //; -59
  "X",        //< -60
  "-...-",    //= -61
  "X",        //> -62
  "..--..",   //? -63
  ".--.-."    //@ -64
};

String SpecialCase[] = {
    "_", "..--.-",   //_ -95
};

void setup() {
  // put your setup code here, to run once:
  Serial.begin(9600);                       //Used for Serial Monitor
  pinMode(buzzerPin, OUTPUT);               //set buzzer pin, OUTPUT automatically sets to LOW
  pinMode(ledPin, OUTPUT);                  //set LED pin, OUTPUT automatically sets to LOW
  lcd.begin(16, 2);
  lcd.print("Hello Friends :)");            //LCD opening sequence
  delay(3000);
  lcd.clear();

}

void loop(){
  // put your main code here, to run repeatedly:
  Serial.println("What would you like to say?");
  Serial.println("");
  lcd.print("Talk to me.");                 //LCD displays this message at the start of each translation

  //Use the Serial Monitor to input a message
  while (Serial.available() == 0){
    //do nothing                            //Wait until user input
  }
  strInput = Serial.readString();           //get the new String
  lcd.clear();
  Serial.print(" Translating: ");
  lcd.print(" *Receiving... ");
  delay(1000);
  lcd.clear();
  Serial.println(strInput);                 //print string to serial monitor
  delay(2000);
  doString(strInput);                       //go to doString function
  delay(3000);
  lcd.clear();

  delay(1000);                              //delay between loops
  Serial.println("");                       //Add a new line feed to serial monitor   
  strInput="";                              //clear the string

}

/* ***********************************************************
 *                Functions - Order is Important
 * *********************************************************** */
void DitOrDah(int DitOrDahDelay){
  //This function turns ON and OFF the LED
  //  and Active Buzzer by the delay value
  //  passed to it.
  digitalWrite(buzzerPin, HIGH);
  digitalWrite(ledPin, HIGH);
  delay(DitOrDahDelay);                    //dit or dah delay
  digitalWrite(buzzerPin, LOW);
  digitalWrite(ledPin, LOW);
}

void doSequence(String sequence){
  //Local Variables
  int strLen = 0;
  char chValue = "";
  
  strLen = sequence.length();               //get length of string
  //note: last char is a NULL
  for (int x = 0; x < strLen; x++){        //loop through all parts
    chValue = sequence.charAt(x);           //get char value
    if(chValue == '.'){
      DitOrDah(dit);                        // do a Dit
      Serial.print(".");                    // print dit
    }
    else if (chValue == '-'){
      DitOrDah(dah);                        // do a Dah
      Serial.print("-");                    // print dah
    }
    else if (chValue == 'X'){               // No translation
      Serial.print("X - no translation");   // print no translation 
    }
    else if (int(chValue) == 0){            // Null char in string
      //Null found                          // May not be needed
      //Do Nothing                          // should be skipped
    } //end if statement

    if (x < strLen) delay(partsDelay);      // gap between parts
  } //end for
  Serial.println("");
} //end doSequence

void translate(char ch){
/* Recall that letters have a numeric ASCII value
 * so you can add and substract them
 * a = 97
 * A = 65
 * 0 = 48
 * 
 * Modify this function for additonal character checks 
 * ASCII numerical order below (mostly)
 */
 
  if (ch >= '!' && ch <= '/'){
    Serial.print(ch);
    Serial.print(" ");
    doSequence(Lpunctuation[ch - '!']);
    lcd.print(ch);                        //writes character into LCD screen
  } 
  else if (ch >= '0' && ch <= '9'){
    Serial.print(ch);
    Serial.print(" ");
    doSequence(numbers[ch - '0']);
    lcd.print(ch); 
  }
  else if (ch >= ':' && ch <= '@'){
    Serial.print(ch);
    Serial.print(" ");
    doSequence(Upunctuation[ch - ':']); 
    lcd.print(ch);    
  } 
  else if (ch >= 'A' && ch <= 'Z'){
    Serial.print(ch);
    Serial.print(" ");
    doSequence(letters[ch - 'A']);
    lcd.print(ch); 
  }
  else if (ch >= 'a' && ch <= 'z'){
    Serial.print(ch);
    Serial.print(" ");
    doSequence(letters[ch - 'a']);
    lcd.print(ch); 
  }
  else if (ch == '_'){
    Serial.print(ch);
    Serial.print(" ");
    doSequence(SpecialCase[1]); 
    lcd.print(ch);    
  }
  
  else {
    Serial.println("");
    Serial.println("End Transmission.");
    //Skip invalid character
  } //end if statement
} //end translate

void doString(String strValue){
  char chValue;                               //Local Variables
  int strLen = 0;

  strLen = strValue.length();                 //get the length of the string

  for(int x = 0; x <= strLen; x++){           //loop through string to get each value
    chValue = strInput.charAt(x);             //get char at position x
 
    if (chValue == " " || int(chValue) == 32){//Found Space between words
      Serial.println("(space)");
      lcd.print(" ");
      delay(wordDelay);                        //delay between words
    } else if (int(chValue) == 13){            //Carrige Return found
      //Do Nothing
    } else if (int(chValue) == 0){             //Null found
      //Do Nothing
    } else {
      translate(chValue);                      //translate character to Morse Code
      if (x < strLen) delay(letterDelay);      //delay between letters
    } //end if
  } //end for
} //end doString
